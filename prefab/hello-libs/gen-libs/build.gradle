apply plugin: 'com.android.library'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                // explicitly build libs
                targets 'gmath', 'gperf'
            }
           
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

for (variant in ["Debug", "Release"]) {
    task "bundle${variant}PrefabAar"(type: Zip) {
        archiveFileName = "gen-libs-prefab-${variant.toLowerCase()}.aar"
        destinationDirectory = file('../distribution')
        into ("prefab") { from fileTree("build/prefab") }
        into ("prefab") { from file("src/main/cpp") include "prefab.json" }
        into ("prefab/modules") { from fileTree("src/main/cpp") include "*/module.json" }
        into ("") { from file("src/main") include "AndroidManifest.xml" }
    }
}

afterEvaluate {
    bundleDebugPrefabAar.dependsOn mergeDebugNativeLibs
    bundleReleasePrefabAar.dependsOn mergeReleaseNativeLibs

    assembleDebug.dependsOn bundleDebugPrefabAar
    assembleRelease.dependsOn bundleReleasePrefabAar
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
}
