apply plugin: 'com.android.library'

android {
    compileSdkVersion 29
    ndkVersion "21.3.6528147"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                // explicitly build libs
                targets 'gmath', 'gperf'
            }
           
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            version '3.10.2'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }

    buildFeatures {
        prefabPublishing true
    }

    prefab {
        gmath {
            headers "src/main/cpp/gmath/src/"
        }
        gperf {
            headers "src/main/cpp/gperf/src/"
        }
    }

    packagingOptions {
        exclude "prefab/modules/gperf/include/gperf.c"
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
}
